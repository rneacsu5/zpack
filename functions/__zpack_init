#!/usr/bin/env zsh

# Output Functions
function __zpack_out() {
    print -P "%B%F{green}\u276F %f%b""$*"
}

function __zpack_out_plain() {
    print "$*"
}

function __zpack_warn() {
    print -P %F{yellow}"$*"%f >&2
}

function __zpack_err() {
    print -P %F{red}"$*"%f >&2
}


# Cache functions
function __zpack_cache() {
    printf "%s\n" "$*" >>$ZPACK_CACHE_FILE
}

function __zpack_cache_var() {
    if [[ ${(Pt)1} = array* ]]; then
        __zpack_cache "$1=( ${(Pq-@)1} )"
    else
        __zpack_cache "$1=${(Pq-)1}"
    fi
}

function __zpack_cache_func() {
    __zpack_cache "$(type -f $1)"
}


# Helper functions
function __zpack_path() {
    path+=($1)
    __zpack_cache "path+=(${(q-)1})"
}

function __zpack_fpath() {
    fpath=($1 $fpath)
    __zpack_cache "fpath=(${(q-)1} \$fpath)"
}

function __zpack_source() {
    local location=$1
    local plugin_id=$2

    if [[ -n $plugin_id ]]; then
        zsh_loaded_plugins+=( $plugin_id )
        __zpack_cache "zsh_loaded_plugins+=( ${(q-)plugin_id} )"
    fi
    ZERO=$location source $location
    __zpack_cache "ZERO=${(q-)location} source ${(q-)location}"
}

function __zpack_run_hook() {
    local name=$1
    local cmd=${(P)name}

    if [[ -n $cmd ]]; then
        pushd -q $dir
        eval $cmd
        popd -q
        __zpack_cache "pushd -q ${(q-)dir}"
        __zpack_cache "eval $cmd"
        __zpack_cache "popd -q"
    fi
}

function __zpack_subcmd() {
    local prefix=$1
    local subcmd=$2

    if [[ -z $prefix ]]; then
        __zpack_err "No prefix given."
        return 1
    fi
    if [[ -z $subcmd ]]; then
        __zpack_err "No subcommand given."
        return 1
    fi
    if [[ ${+functions[__zpack_${prefix}_${subcmd}]} == 0 ]]; then
        __zpack_err "Invalid subcomand given."
        return 1
    fi
    shift 2
    __zpack_${prefix}_${subcmd} $@
}

function __zpack_apply() {
    if [[ -n $ZPACK_CACHE ]]; then
        unset ZPACK_CACHE
        source $ZPACK_CACHE_FILE
    fi
    __zpack_completion apply
}


# Init function
function __zpack_init() {
    # Check cache
    if [[ -f $ZPACK_CACHE_FILE && $ZPACK_CACHE_FILE -nt $HOME/.zshrc ]]; then
        ZPACK_CACHE=1
    else
        # Init Variables
        ZPACK_PLUGINS=()
        ZPACK_RELEASES=()
        ZPACK_SNIPPETS=()
        ZPACK_BUNDLES=()

        # Setup directories and files
        local dir
        for dir in $ZPACK_BIN_DIR $ZPACK_CACHE_DIR; do
            [[ -d $dir ]] || mkdir -p $dir
        done
        : > $ZPACK_CACHE_FILE
        __zpack_cache "#!/usr/bin/env zsh"
        __zpack_path $ZPACK_BIN_DIR

        # Zsh Plugin Standard
        PMSPEC=0fbis
        typeset -ga zsh_loaded_plugins
        __zpack_cache_var PMSPEC
        __zpack_cache "typeset -ga zsh_loaded_plugins"
    fi

    # Init completion
    autoload -Uz __zpack_completion
    __zpack_completion init
}

__zpack_init $@
